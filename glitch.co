lerp = (a, b, alpha=0.5) -> b * alpha + a * (1 - alpha)
lerper-cache = {
	_a: (a, b) -> a
	_b: (a, b) -> b

}
lerper = (alpha) ->
	if alpha <= 0 then return lerper-cache._a
	if alpha >= 1 then return lerper-cache._b
	key = 0 | (alpha * 100)
	if lerper-cache[key] then return that
	beta = 1 - alpha
	return lerper-cache[key] = new Function("a", "b", "return b * #alpha + a * #beta")

zwrap = (num, max) ->
	num += max while num < 0
	return num % max

zclamp = (num, max) ->
	return 0 if num < 0
	return max if num >= max
	return num


randint = (min, max) -> 0|(Math.random! * (max - min) + min)
birandint = (min, max) -> 0|(Math.random! * (max - min) + min) * (if Math.random! <= 0.5 then -1 else +1)
rand = (min, max) -> (Math.random! * (max - min) + min)

make-scangrad = (context, height, brightness=0.1) ->
	grad = context.create-linear-gradient(0, 0, 0, height)
		&height = height
		&add-color-stop 0, "rgba(255, 255, 255, 0)"
		&add-color-stop 0.5, "rgba(255, 255, 255, #brightness)"
		&add-color-stop 1, "rgba(255, 255, 255, 0)"
	return grad



scanlines = !({data, width, height}, multiplier = 0.7) ->
	return if multiplier >= 1
	for y from 0 til height by 2
		for x from 0 til width
			offset = y * width * 4 + x * 4
			data[offset++] *= multiplier
			data[offset++] *= multiplier
			data[offset++] *= multiplier

leak = !({data, width, height}, multiplier = 0.5, magic1 = 0, magic2 = 0) ->
	return if multiplier <= 0
	lerp = lerper multiplier
	dwidth = width * 4
	len = data.length
	for y from 0 til height
		yoffset = y * dwidth + magic1
		for offset from yoffset til yoffset + dwidth by 4
			src = offset + 4 + magic2
			if src >= 0 and src < len and offset >= 0 and offset < len
				data[offset] = lerp data[offset], data[src]

sliceoffset = !({data, width, height}, y0, y1, offset, chanmask=2b111, blend=0.2, drift=0) ->
	return if not chanmask
	lerp = lerper blend
	if offset > 0 then
		x0 = 0
		x1 = width
		dir = +1
	else
		x0 = width - 1
		x1 = 0
		dir = -1

	for y from y0 til y1
		yoff = y * width * 4
		offset += drift
		for x from x0 til x1 by dir
			dst-offset = yoff + x * 4
			src-offset = yoff + zwrap(0 | (x + offset), width) * 4
			if chanmask & 1 then data[dst-offset] = lerp data[dst-offset], data[src-offset]
			if chanmask & 2 then data[dst-offset+1] = lerp data[dst-offset+1], data[src-offset+1]
			if chanmask & 4 then data[dst-offset+2] = lerp data[dst-offset+2], data[src-offset+2]

sliceglitch = !(data, shmin, shmax, offmin, offmax, chanmask, blend) ->
	sh = randint shmin, shmax
	y0 = randint 0, data.height - sh
	sliceoffset data, y0, y0 + sh, birandint(offmin, offmax), chanmask, blend

add-scangrad = !(context, scangrad, time) ->
	{width, height} = context.canvas
	context.save!
	context.translate 0, -scangrad.height + time % (height + scangrad.height * 2)
	context.fill-style = scangrad
	context.fill-rect 0, 0, width, scangrad.height
	context.restore!

bloom = !(context, radius, strength, counter) ->
	return if strength <= 0 or radius <= 0
	{width, height} = context.canvas
	data = context.get-image-data 0, 0, width, height
	blur-data = context.get-image-data 0, 0, width, height
	stackBlurImageData blur-data, 0, 0, width, height, radius
	data-blend blur-data, data, strength, counter, "screen"
	context.put-image-data data, 0, 0

noise = !({data, width, height}, y0, y1, noisiness, min-brightness, max-brightness, replace) ->
	for y from y0 til y1
		yoff = y * width * 4
		for x from 0 til width
			if rand(0, 100) < noisiness
				offset = yoff + x * 4
				brightness = randint min-brightness, max-brightness
				if replace then
					data[offset++] = brightness
					data[offset++] = brightness
					data[offset++] = brightness
				else
					data[offset++] += brightness
					data[offset++] += brightness
					data[offset++] += brightness

displacement-mapper = !(context, displacement-map, scaleX, scaleY) ->
	{width, height} = context.canvas


	document.create-element "canvas"
		& <<< {width, height}
		&get-context "2d"
			&draw-image displacement-map, 0, 0, width, height
			displacement-data = &get-image-data 0, 0, width, height .data


	source-data = context.get-image-data 0, 0, width, height .data
	{data: d} = dest-data = context.get-image-data 0, 0, width, height


	for y from 0 til height
		yoff = y * width * 4
		for x from 0 til width
			offset = yoff + x * 4
			disZ = (displacement-data[offset + 2]) / 127.0
			disX = (displacement-data[offset] - 127) / 128.0 * scaleX * disZ
			disY = (displacement-data[offset + 1] - 127) / 128.0 * scaleY * disZ

			sourceX = 0 | Math.round(x + disX)
			sourceY = 0 | Math.round(y + disY)
			sourceOffset = (zclamp(sourceY, height) * width * 4 + zclamp(sourceX, width) * 4)
			d[offset++] = source-data[sourceOffset++]
			d[offset++] = source-data[sourceOffset++]
			d[offset++] = source-data[sourceOffset++]
			
	context.put-image-data dest-data, 0, 0

# Thanks to http://www.equasys.de/colorconversion.html

to-ycbcr-matrix = [
#	pre post 	red		green	blue
	0,	0,		 0.299,	 0.587,	0.114,
	0,	128,	-0.169,	-0.331,	0.500,
	0,	128,	 0.500,	-0.419,	0.081,
]

from-ycbcr-matrix = [
#	pre post 	red		green	blue
	0,		0,	1,		 0,		 1.4,
	-128,	0,	1,		-0.343, -0.711,
	-128,	0,	1,		 1.765,	 0
]

matrix-xform = !({data, width, height}, matrix) ->
	offset = 0
	[
		r-pre, r-post, r0, r1, r2,
		g-pre, g-post, g0, g1, g2,
		b-pre, b-post, b0, b1, b2,
	] = matrix

	for offset from 0 til data.length by 4
		r = data[offset] + r-pre
		g = data[offset+1] + g-pre
		b = data[offset+2] + b-pre
		data[offset] =   (r0 * r + r1 * g + r2 * b) + r-post
		data[offset+1] = (g0 * r + g1 * g + g2 * b) + g-post
		data[offset+2] = (b0 * r + b1 * g + b2 * b) + b-post

to-ycbcr = !(image-data) -> matrix-xform image-data, to-ycbcr-matrix
from-ycbcr = !(image-data) -> matrix-xform image-data, from-ycbcr-matrix

let

	MARIO_IMAGE = 'data:image/png;base64,iVBORw0KGgoAAAANSUhEUgAAAQAAAADgBAMAAAAeb6VjAAAAMFBMVEUEAgQEqgRclvz82qzcKgT8mjz8vrQ8vvz8/vyE0hTMTgwAAHdANCgM7uRNEgABAABV4TC4AAAFZ0lEQVR42u2cTW+jOBjHDYcqwwm+geVWkdUrHWn2uDtUmjnuqhz2FvXAoV8gPUZopHqPe5xb1EMOfMrxC06AGHASzNNRbIU+NLHxj7//jzH0BRHggjyAB/AAHwqAEVoSxl+M7zFW8s36QKKtqC/blvx7fhxd6mOV4hPW/OAIoOQVeTV5CMEiD2UPoDcJQBrw4g15bgJiSAFNeQ4A7QI02moAZg3ATgdotB0E6AwrOjoLWalucA6AYjcDSJVGTCg3yk4HKNm+ba8HiC0AUa49Kwvqti2zC0vZZAH8RCTg5PireUAapglchYd61KCOHjHDyNF6bum2awFQIaQYOym9GkXaGMv7aruvR0zDo2FZaRwiYmhn9IC0ikqblhEFAFWY5bGdhhUgdWp32/WZsCatJ6WmAqV2sxmA6jw8zlO5dduZAfREYAKgdT1m0HkAgMrqI1nAhZWbGn89HJR0FdB1ThkCVjZ8MJoFhGnXtgyFq0h6QNcxOL1sxu5MSWtznz8PoHe/IvIAHsADfDgAjEJYgE9JDAqA/0+S8LoBkmsHIAGwCeHTkPy8EMwVwIJtQAEQXwKGgACYCYAQGGCzgQV4tRsENwALtmZvgABY9M7gANCCqbIhIQQAZrps8AYCYLEHYAsQANYoGzw/AGoCsEU4NwBm7XJ9AE0PykycHQCLSVhNxTZzkYs0fBOdv4EpwCVY8yvB+lUxhBAAvPv1K38xNv6029G14I2f/1r0jyAAZCqKZNiM9+9oPYD4HMyXpaHF0V0tSkPbhbmrZTn4jYkH8AAewAN4AA/gATyAB/AAHsADXBcAk7/ZQsEAKClVgRsCaAAtARhACQxACTSA/HXSkkECwKahnwk9gAfwAB7AAwAC3KYpKADv/zMoQJqmOTRAmv4BCJA9PuX/ACpwlz8+Peb/QgJkOS9QAJRlHOAJDoBwgPwpy75bVF0Wz/xrwUs7Pl8EQH885mIQLKreV6Krl6rqxPfLPMB+5JmdCR0B0K8PWZb/Bwdwl2ZZ9jckABfgzwQY4IHBAny1A8BIpN4KoU4ML70YZenDNzsFqioybBcD8AIM8Dn9AghwKxT4YucBc5kN4N54/tGlWfAxAAg0wF8ZnAdEGvLJ0DILRIf63PX+pQC30ACc4MFqReQMgK/MrQBceUAS/LTMgq08a6WB3n+fAOAOGoD8FgAuPWCpgLMs8AC/jQeuPQ2vdx44PBd44edris9uAQ63ZKJDU7weAHE/aIozKmDePIBjgL7nAs3oWIGq557osHkAxx6wKG4ViEY1GAMYRXQLgFGSJMN/ruMUAAeJKHEI5QHVfxLE02UB79E+C7DqP0ZD/2biNAB5ztYAwR4gnghgKwEiS4BaAAEwIIFDD9QCIAEQT5MFdZ92WaAFQEkw9M9G3AEEewA5DlMAbGuAyAZAC1AD9ErgzANaAF3iCbIA7RUYzwKcdEs4L0BwBBDPCnAsQJ8EjjwQGADiGbPAJECPBG4AAiNAPBuAWQCzBE48oNcBjb4DkwT3hSrhxFlwWAcc1EcmCZY72f9qaoCgF6AjwfJFAqCJAXDSC9CRYFns9j8DntADzXVA0tmPOwDiDnc7oMA5WdBeB3T3Q/cA7XVAdz92DoCTQYCWBGMAZ3kgSIZLfJICp2cBTsZK6BYgGAWInQKMC9CUwIEHAguAuAEgz2fCLLARoCHBciWPt50OILAC2EuwrI83GYCdAAcJ9Bi/X+KBIioK+egeKKLqphKvCip6AA4Q1W/ARFRFCO3EG0CRK1AUiggmehPCA2z5FVVcoKGiBLhZRRVUREKGm90KLKqdaldARQGwVd/ARLQTTy75BhX9esAD+PWAXw/8AimevooIJ41nAAAAAElFTkSuQmCC'
	PONG_IMAGE = 'data:image/png;base64,iVBORw0KGgoAAAANSUhEUgAAAQAAAADgAQMAAADWjyoTAAAABlBMVEUAAAD///+l2Z/dAAABBklEQVR4Xu3WQYrCQBAF0N9k4bKO4E30ZoNH61l5DY9Q2bkIlMaBQBjsTyzbprH++pH0p6pDsCmRiJH4ARSrpIxVdgtY0haIiaVsauoBgAuIAFBVF0jZCWwGVgJAbSCspqTsAOQVZFgOQDeKXpwObpa/hZxJiwaAHLKjFlGz3xbf93344WBJO0Bb9DyL/lcuWsSwokWsXAxrvL4ZAO1B+xbRAmKGZFOasMtilxIQAsgTUBOccD1g0Dv4VeyfgvEfUGwDSoHlGSQGUACXMhjrA52OfiCvArO/WRztKcADoAy0KTgBy0YNVgJSBPPKfQRgT1rUAvwMq+tfD6QNoKf/ByP5BPimRG5Sg0pghTsM4gAAAABJRU5ErkJggg=='
	C64_IMAGE = 'data:image/png;base64,iVBORw0KGgoAAAANSUhEUgAAAUAAAADIAQMAAACjyqroAAAABlBMVEVAQOCgoPznx/VsAAABO0lEQVR4Xu3SsYpcMQyF4VO5ClurCPMMKbcQeRaB4W/GRbpxYXQffbM4lymGFLdYkkZfq4OE4OiLFffeffpKedMydxf+kgEQPB4MCPEmHEMgAD35JwXHARCKmyAsRNsjPX18UnC/A2tqoZVuIb7t0etGegdmiEPQLMSPl40Agt4ZjH0RMOQhgL9/zZ+vcXdZ6v8p3tCyLscz+66S2VnIRaYrV6Yk3obwhwCLhwSYPQtpgYIISXGbgkNBWBxSg/f3s5BgEWdQi19aed8n51367j/NzkIut+maa05JHFOTrmbNokuNZnYWErNoirk30oYGXRg7COwgxg6iGBHn19Dl5nZ+nXkWcpmlK8jUv1ZW2kQXED4uBgFdsGbiumAGmC4YwbArSZJppi/0oUsqWEoppZRSSimllFJKKaWU3x+6lxF1jMiHAAAAAElFTkSuQmCC'

	width = height = 0

	settings =
		fps: 30
		width: 256
		height: 224
		scale: 2.0
		image: "mario"
		ycbcrPre: false
		ycbcrPost: false

		leaks:
			n-min: 3
			n-max: 7
			strength: 0
			magic1: 0
			magic2: 0

		sliceglitch:
			probability: 0
			n-min: 5
			n-max: 5
			rand-chan: true
			chan-r: true
			chan-g: true
			chan-b: true
			h-min: 5
			h-max: 23
			off-min: 1
			off-max: 25
			drift-prob: 20
			drift-mag: 2

		noise:
			probability: 0
			n-min: 5
			n-max: 5
			h-min: 5
			h-max: 5
			noisiness: 35
			brightness-min: 10
			brightness-max: 50
			replace: false

		bloom:
			radius: 0
			strength: 0.5
			counter: 0.5

		scanlines:
			enabled: false
			brightness: 0.8

		tvscan:
			height: 300
			brightness: 0.0
			speed: 0.3

		afterimage:
			enabled: false
			strengthOut: 0.2
			counterOut: 0.2
			strengthIn: 0.2

		displacement:
			enabled: false
			strength: 15

	afterimage-data = null

	create-displacement = !->
		width = 256
		height = 224
		canvas = document.create-element "canvas"
			& <<< {width, height}
			context = &get-context "2d"
				&fill-style = "black"
				&fill-rect 0, 0, width, height
				data = &get-image-data 0, 0, width, height
				for y from 0 til height
					for x from 0 til width
						ix = ((x / width) - 0.5) * 2
						iy = ((y / height) - 0.5) * 2
						cdis = 1.0 - (ix * ix + iy * iy)
						cdis = Math.cos(cdis * 3.141 / 2.0)
						an = Math.atan2(iy, ix)
						xd = -Math.cos(an) * 125 * cdis
						yd = -Math.sin(an) * 125 * cdis
						offset = y * width * 4 + x * 4
						data.data[offset] = 127 + xd
						data.data[offset+1] = 127 + yd
						data.data[offset+2] = 127

				&put-image-data data, 0, 0
			#document.body.append-child &
		return canvas

	dismap-image = create-displacement!

	draw-debug-grid = !(context)->
		context.begin-path!
		context.fill-style = "white"
		context.fill-rect 0, 0, width, height
		context.stroke-style = "black"
		for x from 0 til width by 8
			context.move-to x, 0
			context.line-to x, height
		
		for y from 0 til height by 8
			context.move-to 0, y
			context.line-to width, y
		context.stroke!


	draw = !->
		t0 = +new Date

		switch settings.image
			case "mario" => context.draw-image mario-image, 0, 0, width, height
			case "pong" => context.draw-image pong-image, 0, 0, width, height
			case "c64" => context.draw-image c64-image, 0, 0, width, height
			case "grid" => draw-debug-grid context

		data = context.get-image-data 0, 0, width, height

		if settings.ycbcrPre then to-ycbcr data


		# ------ DATA GLITCHES


		if randint(0, 100) < settings.sliceglitch.probability then

			for x from 0 til randint(settings.sliceglitch.n-min, settings.sliceglitch.n-max)
				chanmask = 0
				if not settings.sliceglitch.rand-chan or randint(0, 100) < 33 then chanmask |= (+settings.sliceglitch.chan-r)
				if not settings.sliceglitch.rand-chan or randint(0, 100) < 33 then chanmask |= (+settings.sliceglitch.chan-g) << 1
				if not settings.sliceglitch.rand-chan or randint(0, 100) < 33 then chanmask |= (+settings.sliceglitch.chan-b) << 2
				drift = (if randint(0, 100) < settings.sliceglitch.drift-prob then settings.sliceglitch.drift-mag else 0)

				sliceglitch data,
					settings.sliceglitch.h-min, settings.sliceglitch.h-max,
					settings.sliceglitch.off-min, settings.sliceglitch.off-max,
					chanmask, 1, drift


		if randint(0, 100) < settings.noise.probability then
			for x from 0 til randint(settings.noise.n-min, settings.noise.n-max)
				y0 = randint 0, height
				h = randint settings.noise.h-min, settings.noise.h-max
				y1 = y0 + h
				noise data,
					y0, y1,
					settings.noise.noisiness,
					settings.noise.brightness-min, settings.noise.brightness-max,
					settings.noise.replace


		# ------ ANALOG GLITCHES


		for x from 0 til randint(settings.leaks.n-min, settings.leaks.n-max)
			leak data, settings.leaks.strength, settings.leaks.magic1, settings.leaks.magic2

		if settings.bloom.strength
			context.put-image-data data, 0, 0
			bloom context, settings.bloom.radius, settings.bloom.strength, settings.bloom.counter
			data = context.get-image-data 0, 0, width, height
		
		if settings.afterimage.enabled
			if afterimage-data then
				data-blend afterimage-data, data, settings.afterimage.strength-out, settings.afterimage.counter-out, "screen"
			
			if afterimage-data and settings.afterimage.strength-in < 1 then
				data-blend data, afterimage-data, settings.afterimage.strength-in, (1.0 - settings.afterimage.strength-in), "normal"
			else
				afterimage-data := context.get-image-data 0, 0, width, height
		

		# ------ POST GLITCHES

		if settings.scanlines.enabled
			scanlines data, settings.scanlines.brightness 
		
		if settings.ycbcrPost then from-ycbcr data

		context.put-image-data data, 0, 0

		data = null # unusable from here on out

		if settings.tvscan.height and settings.tvscan.brightness then
			scangrad = make-scangrad context, settings.tvscan.height, settings.tvscan.brightness
			add-scangrad context, scangrad, t0 * settings.tvscan.speed

		if settings.displacement.enabled then
			context.begin-path!
			context.stroke-style = "black"
			context.rect 0, 0, width - 1, height - 1
			context.stroke!

			displacement-mapper context, dismap-image, settings.displacement.strength, settings.displacement.strength



		t1 = +new Date
		speed = (t1 - t0)
		set-timeout draw, 1000 / settings.fps

	start = !->
		draw!

	window.benchmark-draws = !->
		t0 = +new Date
		n = 50
		for x from 0 til n
			draw!
		t1 = +new Date
		dur = (t1 - t0) / 1000
		console.log "draws", n
		console.log "duration", dur
		console.log "fps", n / dur



	size-change = ->
		width := canvas.width = settings.width
		height := canvas.height = settings.height
		canvas.style <<< {width: width * settings.scale, height: height * settings.scale}


	canvas = document.create-element "canvas"
		document.body.append-child &
		context = &get-context "2d"

	size-change!


	mario-image = new Image
		&src = MARIO_IMAGE
		&onload = -> start!

	pong-image = new Image
		&src = PONG_IMAGE

	c64-image = new Image
		&src = C64_IMAGE


	gui = new dat.GUI
		&add settings, "fps", 1, 100 .step 1
		&add settings, "scale", 1, 5 .step 0.1
			&on-change -> size-change!
		&add settings, "width", 64, 640 .step 1
			&on-change -> size-change!
		&add settings, "height", 64, 480 .step 1
			&on-change -> size-change!
		&add settings, "image", ["mario", "pong", "grid", "c64"]
		&add settings, "ycbcrPre"
		&add settings, "ycbcrPost"
		&add-folder "leaks"
			&add settings.leaks, "strength", 0, 1
			&add settings.leaks, "nMin", 0, 20 .step 1
			&add settings.leaks, "nMax", 0, 20 .step 1
			&add settings.leaks, "magic1", -10, 10 .step 1
			&add settings.leaks, "magic2", -10, 10 .step 1
		&add-folder "sliceglitch"
			&add settings.sliceglitch, "probability", 0, 100 .step 1
			&add settings.sliceglitch, "nMin", 0, 100 .step 1
			&add settings.sliceglitch, "nMax", 0, 100 .step 1
			&add settings.sliceglitch, "randChan"
			&add settings.sliceglitch, "chanR"
			&add settings.sliceglitch, "chanG"
			&add settings.sliceglitch, "chanB"
			&add settings.sliceglitch, "hMin", 1, 300 .step 1
			&add settings.sliceglitch, "hMax", 1, 300 .step 1
			&add settings.sliceglitch, "offMin", 0, 100 .step 1
			&add settings.sliceglitch, "offMax", 0, 100 .step 1
			&add settings.sliceglitch, "driftProb", 0, 100 .step 1
			&add settings.sliceglitch, "driftMag", -10, 10 .step 1
		&add-folder "noise"
			&add settings.noise, "probability", 0, 100 .step 1
			&add settings.noise, "noisiness", 0, 100 .step 1
			&add settings.noise, "nMin", 0, 100 .step 1
			&add settings.noise, "nMax", 0, 100 .step 1
			&add settings.noise, "hMin", 1, 300 .step 1
			&add settings.noise, "hMax", 1, 300 .step 1
			&add settings.noise, "brightnessMin", -255, 255 .step 1
			&add settings.noise, "brightnessMax", -255, 255 .step 1
			&add settings.noise, "replace"
		&add-folder "bloom"
			&add settings.bloom, "strength", 0, 1
			&add settings.bloom, "radius", 0, 10 .step 1
			&add settings.bloom, "counter", 0, 1
		&add-folder "scanlines"
			&add settings.scanlines, "enabled"
			&add settings.scanlines, "brightness", 0, 1
		&add-folder "tvscan"
			&add settings.tvscan, "brightness", 0, 1			
			&add settings.tvscan, "height", 0, 500 .step 1
			&add settings.tvscan, "speed", 0, 1
		&add-folder "afterimage"
			&add settings.afterimage, "enabled"
			&add settings.afterimage, "strengthOut", 0, 1
			&add settings.afterimage, "counterOut", 0, 1
			&add settings.afterimage, "strengthIn", 0, 1
		&add-folder "displacement"
			&add settings.displacement, "enabled"
			&add settings.displacement, "strength", -50, 50 .step 1